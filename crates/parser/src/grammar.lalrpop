use crate::ast::*;
use crate::token::Token;
use crate::{Error, Location};

grammar<'a>;

pub Program: Program = {
    <statements: SepEndBy<Stmt, NEWLINE>> => Program { statements },
};

Stmt: Stmt = {
    <annotations: Annotations> "def" <name: Name> "(" <params: SepEndBy<Param, ",">> ")" ":" <return_type: Name> "=" <body: Expr> =>
        Stmt::Def {
            annotations,
            name,
            params,
            return_type,
            body: Box::new(body),
        },
    Expr => Stmt::Expr(<>),
};

Annotations: Vec<Expr> = {
    ("@" <Expr> NEWLINE?)* => <>,
};

Param: (String, String) = {
    <Name> ":" <Name> => (<>),
};

Expr: Expr = {
    AddExpr,
    LambdaExpr,
};

AddExpr: Expr = {
    <lhs: AddExpr> <op: AddOp> <rhs: MulExpr> =>
        Expr::BinOp { op, lhs: Box::new(lhs), rhs: Box::new(rhs) },
    MulExpr,
};

AddOp: BinOp = {
    "+" => BinOp::Add,
    "-" => BinOp::Sub,
};

MulExpr: Expr = {
    <lhs: MulExpr> <op: MulOp> <rhs: CallExpr> =>
        Expr::BinOp { op, lhs: Box::new(lhs), rhs: Box::new(rhs) },
    CallExpr,
};

MulOp: BinOp = {
    "*" => BinOp::Mul,
};

CallExpr: Expr = {
    <callee: CallExpr> "(" <args: SepEndBy<Expr, ",">> ")" =>
        Expr::Call { callee: Box::new(callee), args },
    Term,
};

LambdaExpr: Expr = {
    "|" <params: SepEndBy<Name, ",">> "|" <body: Expr> =>
        Expr::Lambda { params, body: Box::new(body) },
};

Term: Expr = {
    <Name> => Expr::Ident(<>),
    INT => Expr::IntLit(<>.to_string()),
    STRING => Expr::StringLit(<>.to_string()),
    "(" <Expr> ")" => <>,
};

Name: String = IDENT => <>.to_string();

#[inline]
SepEndBy<T, U>: Vec<T> = {
    <items: (<T> U)*> <last: T?> => {
        let mut items = items;
        items.extend(last);
        items
    },
};

extern {
    type Location = Location;
    type Error = Error;

    enum Token<'a> {
        IDENT => Token::Ident(<&'a str>),
        INT => Token::Int(<&'a str>),
        STRING => Token::String(<&'a str>),
        "+" => Token::Plus,
        "-" => Token::Minus,
        "*" => Token::Star,
        "/" => Token::Slash,
        "%" => Token::Percent,
        "^" => Token::Caret,
        "!" => Token::Not,
        "&" => Token::And,
        "|" => Token::Or,
        "=" => Token::Eq,
        "==" => Token::EqEq,
        ">" => Token::Gt,
        "<" => Token::Lt,
        ">=" => Token::Ge,
        "<=" => Token::Le,
        "->" => Token::RArrow,
        "=>" => Token::FatArrow,
        "@" => Token::At,
        "(" => Token::LParen,
        ")" => Token::RParen,
        "[" => Token::LBrace,
        "]" => Token::RBrace,
        "{" => Token::LBracket,
        "}" => Token::RBracket,
        "." => Token::Dot,
        "," => Token::Comma,
        ";" => Token::Semi,
        ":" => Token::Colon,
        NEWLINE => Token::Nl,
        "def" => Token::KwDef,
        "let" => Token::KwLet,
    }
}
